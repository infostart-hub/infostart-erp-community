
// Добавление группы отбора
// Параметры:
//  КоллекцияЭлементов - контейнер с элементами и группами отбора, например
//                      Список.Отбор.Элементы или группа в отборе.
//  ТипГруппы - ТипГруппыЭлементовОтбораКомпоновкиДанных - тип группы.
//  Представление строка - представление группы.
//
Функция СоздатьГруппуЭлементовОтбора(КоллекцияЭлементов, Представление, ТипГруппы) Экспорт
	
	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;
	
	ГруппаЭлементовОтбора.Представление    = Представление;
	ГруппаЭлементовОтбора.Применение       = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаЭлементовОтбора.ТипГруппы        = ТипГруппы;
	ГруппаЭлементовОтбора.Использование    = Истина;
	
	Возврат ГруппаЭлементовОтбора;
	
КонецФункции

// Процедура изменения элемента отбора в группе элементов отбора.
//
Процедура ИзменитьЭлементОтбораГруппыСписка(Группа, ИмяПоля, ПравоеЗначение = Неопределено, Установить = Ложь, ВидСравнения = Неопределено) Экспорт
	
	УдалитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля);
	
	Если Установить Тогда
		УстановитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля, ПравоеЗначение, ВидСравнения);
	КонецЕсли;
	
КонецПроцедуры

// Процедура установки элемента отбора в группе элементов отбора.
//
Процедура УстановитьЭлементОтбораГруппыСписка(Группа, ИмяПоля, ПравоеЗначение = Неопределено, Установить = Ложь, ВидСравнения = Неопределено) Экспорт
	
	Если Установить Тогда
		УстановитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля, ПравоеЗначение, ВидСравнения);
	Иначе
		УдалитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля);
	КонецЕсли;
	
КонецПроцедуры // ИзменитьЭлементОтбораСписка()

// Процедура копирования элемента отбора в группе элементов отбора.
//
Процедура СкопироватьЭлементыОтбораВГруппуСписка(ГруппаЭлементов, КоллекцияЭлементовИсточник, ПроверятьДоступность = Ложь, ОчищатьПриемник = Истина) Экспорт
	
	ГруппаЭлементов.Элементы.Очистить();
	
	Для каждого ЭлементКоллекции Из КоллекцияЭлементовИсточник Цикл
		ЗаполнитьЗначенияСвойств(ГруппаЭлементов.Элементы.Добавить(ТипЗнч(ЭлементКоллекции)), ЭлементКоллекции);
	КонецЦикла;
	
КонецПроцедуры

// Удаляет элемент отбора динамического списка.
//
// Параметры:
// Список  - обрабатываемый динамический список,
// ИмяПоля - имя поля компоновки, отбор по которому нужно удалить.
//
Процедура УдалитьЭлементОтбораСписка(Список, ИмяПоля) Экспорт
	
	УдалитьЭлементОтбораКоллекции(Список.Отбор.Элементы, ИмяПоля);
	
КонецПроцедуры // УдалитьЭлементОтбораСписка()

Процедура УстановитьЭлементОтбораКоллекции(КоллекцияЭлементов, ИмяПоля, ПравоеЗначение, ВидСравнения = Неопределено) Экспорт
	ЭлементОтбора=НайтиЭлементОтбораПоПолю(КоллекцияЭлементов, ИмяПоля);
	Если ЭлементОтбора=Неопределено Тогда
		ЭлементОтбора=КоллекцияЭлементов.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение=Новый ПолеКомпоновкиДанных(ИмяПоля);
		ЭлементОтбора.ВидСравнения=?(ВидСравнения=Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравнения);
		ЭлементОтбора.РежимОтображения=РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	ЭлементОтбора.Использование=Истина;	
	ЭлементОтбора.ПравоеЗначение=ПравоеЗначение;
КонецПроцедуры

// Устанавливает элемент отбор динамического списка.
//
// Параметры:
// Список			- обрабатываемый динамический список,
// ИмяПоля			- имя поля компоновки, отбор по которому нужно установить,
// ВидСравнения		- вид сравнения отбора, по умолчанию - Равно,
// ПравоеЗначение 	- значение отбора.
//
Процедура УстановитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение, ВидСравнения=Неопределено) Экспорт
	УстановитьЭлементОтбораКоллекции(Список.Отбор.Элементы, ИмяПоля, ПравоеЗначение, ВидСравнения);	
КонецПроцедуры

// Изменяет элемент отбора динамического списка.
//
// Параметры:
// Список         - обрабатываемый динамический список,
// ИмяПоля        - имя поля компоновки, отбор по которому нужно установить,
// ВидСравнения   - вид сравнения отбора, по умолчанию - Равно,
// ПравоеЗначение - значение отбора,
// Установить     - признак необходимости установить отбор.
//
Процедура ИзменитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение = Неопределено, Установить = Ложь, ВидСравнения = Неопределено) Экспорт
	
	УдалитьЭлементОтбораСписка(Список, ИмяПоля);
	
	Если Установить Тогда
		УстановитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение, ВидСравнения);
	КонецЕсли;
	
КонецПроцедуры // ИзменитьЭлементОтбораСписка()

// Устанавливает значение Значение параметра ИмяПараметра динамического списка Список 
// или отключает его использование в зависимости от признака Использование.
//
Процедура УстановитьПараметрДинамическогоСписка(Список, ИмяПараметра, Значение, Использование = Истина) Экспорт
	
	Если Использование Тогда
		ЗначениеПараметра = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		Если ЗначениеПараметра <> Неопределено Тогда
			Список.Параметры.УстановитьЗначениеПараметра(ИмяПараметра, Значение);
		КонецЕсли;
	Иначе
		
		ЗначениеПараметра = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		Если ЗначениеПараметра <> Неопределено Тогда
			Если ЗначениеПараметра.Значение <> Значение Тогда
				ЗначениеПараметра.Значение = Значение;
			КонецЕсли;
			Если ЗначениеПараметра.Использование <> Использование Тогда
				ЗначениеПараметра.Использование = Использование;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Поиск элементов и групп отбора динамического списка.
// Параметры:
// ОбластьПоиска - контейнер с элементами и группами отбора, например
//					Список.Отбор
//					или группа в отборе.
// ИмяПоля - имя поля компоновки (не используется для групп).
// Представление - представление поля компоновки.
// Примечание: поиск может осуществляться или по ЛевомуЗначению, или по Представлению.
//
Функция НайтиЭлементыИГруппыОтбора(Знач ОбластьПоиска,
									Знач ИмяПоля = Неопределено,
									Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Возврат МассивЭлементов;
	
КонецФункции

Процедура НайтиРекурсивно(КоллекцияЭлементов, МассивЭлементов, СпособПоиска, ЗначениеПоиска)
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если СпособПоиска = 1 Тогда
				Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			ИначеЕсли СпособПоиска = 2 Тогда
				Если ЭлементОтбора.Представление = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			НайтиРекурсивно(ЭлементОтбора.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
			
			Если СпособПоиска = 2 И ЭлементОтбора.Представление = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЭлементОтбораКоллекции(КоллекцияЭлементов, ИмяПоля) Экспорт
	ПолеКомпоновки=Новый ПолеКомпоновкиДанных(ИмяПоля);
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементОтбора)=Тип("ЭлементОтбораКомпоновкиДанных") И ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда
			КоллекцияЭлементов.Удалить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры 

Функция НайтиЭлементОтбораПоПолю(КоллекцияЭлементов, ИмяПоля) Экспорт
	ПолеКомпоновки=Новый ПолеКомпоновкиДанных(ИмяПоля);
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементОтбора)=Тип("ЭлементОтбораКомпоновкиДанных") И ЭлементОтбора.ЛевоеЗначение=ПолеКомпоновки Тогда
			Возврат ЭлементОтбора;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено; 
КонецФункции

Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление)
	
	ВозвращаемоеЗначение = Неопределено;
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение
	
КонецФункции

Функция НайтиЭлементОтбораПоЗначению(КомпоновщикНастроек, стрАтрибут) Экспорт
	ПолеАтрибута=Новый ПолеКомпоновкиДанных(стрАтрибут);
	Для каждого СтрокаКоллекции Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(СтрокаКоллекции)=Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если СтрокаКоллекции.ЛевоеЗначение=ПолеАтрибута Тогда
				Возврат СтрокаКоллекции;
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

Функция НайтиЭлементДоступностиОтбора(КоллекцияЭлементов, ИмяПоля) Экспорт
	ПолеКомпоновки=Новый ПолеКомпоновкиДанных(ИмяПоля);
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Поле=ПолеКомпоновки Тогда
			Возврат ЭлементОтбора;
		КонецЕсли;
	КонецЦикла;	
	Возврат Неопределено;
КонецФункции