&НаКлиенте
Перем СтруктураСохраняемыхРеквизитов;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ЗначенияЗаполнения);
		Если ЗначениеЗаполнено(ПроцентУменьшенияСуммыНалога) Тогда
			ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах;
		ИначеЕсли ЗначениеЗаполнено(СуммаУменьшенияСуммыНалога) Тогда
			ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму;
		Иначе
			ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.НеПрименяется;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СниженнаяНалоговаяСтавка) Тогда
			СнижениеНалоговойСтавки = 1;
		КонецЕсли;			
	КонецЕсли;	
	УправлениеФормой(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СтруктураСохраняемыхРеквизитов = Новый Структура;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ=Истина; Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект, Отказ);
		ПоказатьВопрос(ОповещениеОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НалоговаяЛьготаПоНалоговойБазе <> Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395");
	КонецЕсли;
		
	Если НалоговаяЛьготаПоНалоговойБазе <> Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеОблагаемаяНалогомПлощадь Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоляНеОблагаемойНалогомПлощадиЧислитель");
		МассивНепроверяемыхРеквизитов.Добавить("ДоляНеОблагаемойНалогомПлощадиЗнаменатель");
	КонецЕсли;
		
	Если НалоговаяЛьготаПоНалоговойБазе <> Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391");
		МассивНепроверяемыхРеквизитов.Добавить("НеОблагаемаяНалогомСумма");
	Иначе
		Если НЕ УменьшениеНалоговойБазыПоСтатье391 Тогда
			МассивНепроверяемыхРеквизитов.Добавить("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391");
		КонецЕсли;
		Если НЕ УменьшениеНалоговойБазыНаСумму И Период < '20170101' Тогда
			МассивНепроверяемыхРеквизитов.Добавить("НеОблагаемаяНалогомСумма");
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантУменьшенияСуммыНалога <> Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроцентУменьшенияСуммыНалога");
	КонецЕсли;		
		
	Если ВариантУменьшенияСуммыНалога <> Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СуммаУменьшенияСуммыНалога");
	КонецЕсли;
	
	Если НЕ СнижениеНалоговойСтавки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СниженнаяНалоговаяСтавка");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяЛьготаПоНалоговойБазеПриИзменении(Элемент)
	Если Элементы.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395", КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395);
	КонецЕсли;
	Если Элементы.ДоляНеОблагаемойНалогомПлощадиЧислитель.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("ДоляНеОблагаемойНалогомПлощадиЧислитель", ДоляНеОблагаемойНалогомПлощадиЧислитель);
	КонецЕсли;
	Если Элементы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("ДоляНеОблагаемойНалогомПлощадиЗнаменатель", ДоляНеОблагаемойНалогомПлощадиЗнаменатель);
	КонецЕсли;
	Если Элементы.УменьшениеНалоговойБазыПоСтатье391.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("УменьшениеНалоговойБазыПоСтатье391", УменьшениеНалоговойБазыПоСтатье391);
	КонецЕсли;
	Если Элементы.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391", КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391);
	КонецЕсли;
	Если Элементы.УменьшениеНалоговойБазыНаСумму.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("УменьшениеНалоговойБазыНаСумму", УменьшениеНалоговойБазыНаСумму);
	КонецЕсли;
	Если Элементы.НеОблагаемаяНалогомСумма.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("НеОблагаемаяНалогомСумма", НеОблагаемаяНалогомСумма);
	КонецЕсли;
	
	КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = "";
	ДоляНеОблагаемойНалогомПлощадиЧислитель = 0;
	ДоляНеОблагаемойНалогомПлощадиЗнаменатель = 0;
	УменьшениеНалоговойБазыПоСтатье391 = Ложь;
	КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 = "";
	УменьшениеНалоговойБазыНаСумму = Ложь;
	НеОблагаемаяНалогомСумма = 0;
	
	Если НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395")
		И СтруктураСохраняемыхРеквизитов.Свойство("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395") Тогда
		КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = СтруктураСохраняемыхРеквизитов.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395;
	ИначеЕсли НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеОблагаемаяНалогомПлощадь") Тогда
		Если СтруктураСохраняемыхРеквизитов.Свойство("ДоляНеОблагаемойНалогомПлощадиЧислитель") Тогда
			ДоляНеОблагаемойНалогомПлощадиЧислитель = СтруктураСохраняемыхРеквизитов.ДоляНеОблагаемойНалогомПлощадиЧислитель;
		КонецЕсли;
		Если СтруктураСохраняемыхРеквизитов.Свойство("ДоляНеОблагаемойНалогомПлощадиЗнаменатель") Тогда
			ДоляНеОблагаемойНалогомПлощадиЗнаменатель = СтруктураСохраняемыхРеквизитов.ДоляНеОблагаемойНалогомПлощадиЗнаменатель;
		КонецЕсли;
	ИначеЕсли НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы") Тогда 
		Если СтруктураСохраняемыхРеквизитов.Свойство("УменьшениеНалоговойБазыПоСтатье391") Тогда
			УменьшениеНалоговойБазыПоСтатье391 = СтруктураСохраняемыхРеквизитов.УменьшениеНалоговойБазыПоСтатье391;
		КонецЕсли;
		Если СтруктураСохраняемыхРеквизитов.Свойство("УменьшениеНалоговойБазыНаСумму") Тогда
			УменьшениеНалоговойБазыНаСумму = СтруктураСохраняемыхРеквизитов.УменьшениеНалоговойБазыНаСумму;
		КонецЕсли;
		Если УменьшениеНалоговойБазыПоСтатье391 
			И СтруктураСохраняемыхРеквизитов.Свойство("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391") Тогда
			КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 = СтруктураСохраняемыхРеквизитов.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391;
		КонецЕсли;
		Если (УменьшениеНалоговойБазыНаСумму Или Период >= '20170101')
			И СтруктураСохраняемыхРеквизитов.Свойство("НеОблагаемаяНалогомСумма") Тогда
			НеОблагаемаяНалогомСумма = СтруктураСохраняемыхРеквизитов.НеОблагаемаяНалогомСумма;
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы=Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",		"РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта",	"РегистрацияЗемельныхУчастков");
	ПараметрыФормы.Вставить("НазваниеМакета",	"ЛьготыПоОсвобождениюОтЗемельногоНалога");
	ПараметрыФормы.Вставить("ТекущийПериод",	ЭтаФорма.Период);

	ДополнительныеПараметры=Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	
	ОповещениеОЗакрытии=Новый ОписаниеОповещения("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395НачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);	
КонецПроцедуры

&НаКлиенте
Процедура УменьшениеНалоговойБазыПоСтатье391ПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы=Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",		"РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта",	"РегистрацияЗемельныхУчастков");
	ПараметрыФормы.Вставить("НазваниеМакета",	"ЛьготыПоУменьшениюЗемельногоНалога");
	ПараметрыФормы.Вставить("ТекущийПериод",	ЭтаФорма.Период);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391НачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);	
КонецПроцедуры

&НаКлиенте
Процедура УменьшениеНалоговойБазыНаСуммуПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВариантУменьшенияСуммыНалогаПриИзменении(Элемент)
	Если Элементы.ПроцентУменьшенияСуммыНалога.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("ПроцентУменьшенияСуммыНалога", ПроцентУменьшенияСуммыНалога);
	КонецЕсли;
	Если Элементы.СуммаУменьшенияСуммыНалога.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("СуммаУменьшенияСуммыНалога", СуммаУменьшенияСуммыНалога);
	КонецЕсли;
	
	ПроцентУменьшенияСуммыНалога = 0;
	СуммаУменьшенияСуммыНалога = 0;
	
	Если ВариантУменьшенияСуммыНалога = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах")
		И СтруктураСохраняемыхРеквизитов.Свойство("ПроцентУменьшенияСуммыНалога") Тогда
		ПроцентУменьшенияСуммыНалога = СтруктураСохраняемыхРеквизитов.ПроцентУменьшенияСуммыНалога;
	ИначеЕсли ВариантУменьшенияСуммыНалога = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму")
		И СтруктураСохраняемыхРеквизитов.Свойство("СуммаУменьшенияСуммыНалога") Тогда
		СуммаУменьшенияСуммыНалога = СтруктураСохраняемыхРеквизитов.СуммаУменьшенияСуммыНалога;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СнижениеНалоговойСтавкиПриИзменении(Элемент)
	Если Элементы.СниженнаяНалоговаяСтавка.Доступность Тогда
		СтруктураСохраняемыхРеквизитов.Вставить("СниженнаяНалоговаяСтавка", СниженнаяНалоговаяСтавка);
	КонецЕсли;
	
	СниженнаяНалоговаяСтавка=0;	
	Если СнижениеНалоговойСтавки И СтруктураСохраняемыхРеквизитов.Свойство("СниженнаяНалоговаяСтавка") Тогда
		СниженнаяНалоговаяСтавка=СтруктураСохраняемыхРеквизитов.СниженнаяНалоговаяСтавка;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Отказ=Ложь;
	ЗаписатьДанные(Отказ);	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Модифицированность=Ложь;
	Закрыть();	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Элементы=Форма.Элементы;	
	Элементы.НалоговаяЛьготаПоНалоговойБазеУменьшениеНалоговойБазыНаСумму.Видимость = Форма.Период >= '20170101';
	Элементы.НалоговаяЛьготаПоНалоговойБазеУменьшениеНалоговойБазы.Видимость = Форма.Период < '20170101';
	Элементы.ГруппаУменьшениеНалоговойБазыПоСтатье391.Видимость = Форма.Период < '20170101';
	Элементы.УменьшениеНалоговойБазыНаСумму.Видимость = Форма.Период < '20170101';
	
	Элементы.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395.Доступность=Форма.НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395");
	Элементы.ДоляНеОблагаемойНалогомПлощадиЧислитель.Доступность=Форма.НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеОблагаемаяНалогомПлощадь");
	Элементы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель.Доступность=Форма.НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеОблагаемаяНалогомПлощадь");
	Элементы.УменьшениеНалоговойБазыПоСтатье391.Доступность=Форма.НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы");
	Элементы.УменьшениеНалоговойБазыНаСумму.Доступность=Форма.НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы");
	Элементы.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391.Доступность=Форма.НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы") И Форма.УменьшениеНалоговойБазыПоСтатье391;
	Элементы.НеОблагаемаяНалогомСумма.Доступность=Форма.НалоговаяЛьготаПоНалоговойБазе = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы") И (Форма.УменьшениеНалоговойБазыНаСумму Или Форма.Период >= '20170101');
	Элементы.ПроцентУменьшенияСуммыНалога.Доступность=Форма.ВариантУменьшенияСуммыНалога = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах");	
	Элементы.СуммаУменьшенияСуммыНалога.Доступность=Форма.ВариантУменьшенияСуммыНалога = ПредопределенноеЗначение("Перечисление.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму");
	Элементы.СниженнаяНалоговаяСтавка.Доступность=Форма.СнижениеНалоговойСтавки;		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Отказ) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьДанные(Отказ);

	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура ЗаписатьДанные(Отказ = Ложь)
	Если ПроцентУменьшенияСуммыНалога > 100 Тогда
		ТекстСообщения = НСтр("ru = 'Льгота в виде уменьшения суммы налога не может превышать 100%!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПроцентУменьшенияСуммыНалога", , Отказ);
	КонецЕсли;
	
	Если СниженнаяНалоговаяСтавка > НалоговаяСтавка Тогда
		ШаблонСообщения = НСтр("ru = 'Сниженная налоговая ставка не может превышать основную налоговую ставку %1%!'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Формат(НалоговаяСтавка, "ЧЦ=5; ЧДЦ=4"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СниженнаяНалоговаяСтавка", , Отказ);
	КонецЕсли;
	
	Если Отказ Тогда Возврат; КонецЕсли;
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЛьготы=Новый Структура("ВариантУменьшенияСуммыНалога, 
		|ЛьготнаяСтавка, 
		|ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 
		|ДоляНеОблагаемойНалогомПлощадиЧислитель, 
		|КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395, 
		|КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391, 
		|НалоговаяЛьготаПоНалоговойБазе, 
		|НеОблагаемаяНалогомСумма, 
		|ПроцентУменьшенияСуммыНалога, 
		|СниженнаяНалоговаяСтавка, 
		|СуммаУменьшенияСуммыНалога, 
		|УменьшениеНалоговойБазыНаСумму, 
		|УменьшениеНалоговойБазыПоСтатье391");
	ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, ЭтаФорма);
	
	Модифицированность = Ложь;
	
	ОповеститьОВыборе(ПараметрыЛьготы);	
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395НачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Элемент = ДополнительныеПараметры.Элемент;
	КодЛьготы = РезультатЗакрытия;
	Если КодЛьготы <> Неопределено Тогда
		Элемент.ВыделенныйТекст = КодЛьготы;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391НачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Элемент = ДополнительныеПараметры.Элемент;
	КодЛьготы = РезультатЗакрытия;
	Если КодЛьготы <> Неопределено Тогда
		Элемент.ВыделенныйТекст = КодЛьготы;
	КонецЕсли;	
КонецПроцедуры